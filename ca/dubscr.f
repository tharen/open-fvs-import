      SUBROUTINE DUBSCR(ISPC,D,H,RDEN,QMDPLT,CR,TPCT,TPCCF)
      IMPLICIT NONE
C----------
C CA $Id$
C----------
C
C  THIS SUBROUTINE CALCULATES CROWN RATIOS FOR TREES
C  IN THE INVENTORY THAT ARE MISSING CROWN RATIO
C  MEASUREMENTS AND ARE LESS THAN 1.0 INCH DBH.
C
C  FIRS AND SPRUCES USE BCR_(1) COEFFICIENTS
C  DOUGLAS-FIR USES BCR_(2) COEFFICIENTS
C  CEDARS AND HEMLOCKS USE BCR_(3) COEFFICIENTS
C  PINES, GIANT SEQUOIA, AND PACIFIC YEW USE BCR_(4) COEFFICIENTS
C  HARDWOODS USE BCR_(5) COEFFICIENTS (CONSTANT ICR=5)
C  JUNIPER USE BCR_(6) COEFFICIENTS (CONSTANT ICR=9)
C
C----------
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'ARRAYS.F77'
C
C
      INCLUDE 'PLOT.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
      INCLUDE 'PDEN.F77'
C
C
COMMONS
C
      LOGICAL DEBUG
      EXTERNAL RANN
      REAL BCR0(7),BCR1(7),BCR2(7),CRSD(7)
      INTEGER IMAP(MAXSP),ISPC,IISPC
      REAL TPCCF,TPCT,CR,H,D,SD,FCR,BACHLO,RDEN,QMDPLT
      REAL B1,B2,B3,B4,B5,HDR
      REAL RDANUW
C
      DATA IMAP/3*3, 3*1, 2, 2*3, 11*4, 6, 1, 7, 2*4, 24*5, 7/
C
      DATA BCR0/
     & 8.042774, 8.477025, 7.558538, 6.489813, 5.000000, 9.000000,
     & 0.000000/
      DATA BCR1/
     & 0.007198, -0.018033, -0.015637, -0.029815, 0.000000, 0.000000,
     & 0.000000/
      DATA BCR2/
     & -0.016163, -0.018140, -0.009064, -0.009276, 0.000000, 0.000000,
     & 0.000000/
      DATA CRSD/
     & 1.3167, 1.3756, 1.9658, 2.0426, 0.5, 0.5, 0.15/
     
C    RW AND GS COEFFICIENTS
      B1 = -1.842864
      B2 = 0.007664
      B3 = 0.487667
      B4 = 0.823697
      B5 = -0.145448
C-----------
C  CHECK FOR DEBUG.
C-----------
      CALL DBCHK (DEBUG,'DUBSCR',6,ICYC)
C----------
C  DUMMY ARGUMENT NOT USED WARNING SUPPRESSION SECTION
C----------
      RDANUW = D
      RDANUW = TPCCF
      RDANUW = TPCT
C----------
C  EXPECTED CROWN RATIO IS A FUNCTION OF SPECIES, HEIGHT,
C  AND BASAL AREA. THE EQUATION RETURNS A CROWN CODE VALUE 0-9
C  FOR GS AND RW, CROWN RATIO IS A FUNCTION OF DBH, HEIGHT-DIAMETER RATIO,
C  RELATIVE DENSITY, AND RELATIVE DIAMETER (D/QMD)
C----------
      HDR = (H*12)/D
      IF(HDR .GT. 150) HDR = 150
      
C     SELECT SPECIES INDEX
      IISPC=IMAP(ISPC)
      
C RW AND GS CR CALCULATION
      IF(ISPC .EQ. 23 .OR. ISPC .EQ. 50) THEN
        CR = B1 + B2*D + B3*LOG(HDR) + B4*RDEN + B5*(D/QMDPLT)
        CR = 1/(1 + EXP(CR))
C     MARK CASTLE: DEBUG
        IF(DEBUG)WRITE(JOSTND,*)'IN DUBSCR',' D=',D,' H=',H,
     &  ' RDEN=',RDEN,' QMDPLT=',QMDPLT,' HDR=', HDR,' CR=',CR

C ALL OTHER SPECIES CR CALCULATION
      ELSE
C        IISPC=IMAP(ISPC)
        CR=BCR0(IISPC) + BCR1(IISPC)*H + BCR2(IISPC)*BA
      ENDIF
C----------
C  ASSIGN A RANDOM ERROR TO THE CROWN RATIO PREDICTION
C  VALUES OF CRSD ARE THE STANDARD ERROR VALUES FROM REGRESSION
C----------
      SD=CRSD(IISPC)
   10 FCR=BACHLO(0.0,SD,RANN)
      IF(ABS(FCR).GT.SD) GO TO 10
      CR=CR+FCR
C----------
C  CONVERT CODE VALUE TO A CROWN RATIO PROPORTION (0-1)
C----------
C ADJUST CR  IF SPECIES IS NOT RW/GS
      IF(ISPC .NE. 23 .OR. ISPC .NE. 50) CR=((CR-1.0)*10.0 + 1.0)/100.
      IF(CR .GT. .95) CR=.95
      IF(CR .LT. .05) CR=.05
      IF(DEBUG)WRITE(JOSTND,600)ISPC,H,BA,CR,FCR
  600 FORMAT(' IN DUBSCR, ISPC= ',I2,' H= ',F5.1,' BA= ',F9.4,
     &' CR= ',F4.3,' RAN ERR= ',F6.4)
      RETURN
      END
