      SUBROUTINE HTGR5(ISP,SSITE,BAA,YHTG,RELHT,CR,H,PBAL,TEMASP,
     &                 TEMSLP)
      IMPLICIT NONE
C----------
C NC $Id$
C----------
C
C SUBROUTINE TO CALCULATE 5-YEAR HEIGHT GROWTH
C SPECIES INDEX 1-OC, 2-WP, 3-DF, 4-WF, 5-M , 6-IC, 7-BO,
C               8-TO, 9-RF, 10-PP, 11-OTHER, 12 - RW
C
COMMONS
C
C
      INCLUDE 'PRGPRM.F77'
C
C
      INCLUDE 'CONTRL.F77'
C
C
COMMONS
C----------
      REAL HRELHT(12),HCRSQ(12),HHT(12),HBA(12),HSITE(12),HCON(12)
      REAL H,CR,RELHT,YHTG,BAA,SSITE,HTGR
      REAL PBAL,TEMASP,TEMSLP
      INTEGER IMETH(12),ISP
      DATA HRELHT/11*4.292, 0.0/
      DATA HCRSQ/11*0.0566, 0.0/
      DATA HHT/11*0.1699, 0.0/
      DATA HBA/4*-0.00828,-0.54648,-0.00828,-0.78296,-0.58984,
     &         2*-0.00828,-0.54984, 0.0/
      DATA HSITE/11*0.00768, 0.0/
      DATA IMETH/1,1,1,1,2,1,2,2,1,1,2,3/
      DATA HCON/4*-2.193,3.560,-2.193,3.817,3.385,2*-2.193,
     &          3.385, 0.0/

C  CALCULATE HTGR FOR SPECIES 2
      IF(IMETH(ISP) .EQ. 2) THEN
        IF(BAA .LE. 5.0)BAA=5.0
        HTGR = EXP(HCON(ISP) + HBA(ISP)*ALOG(BAA))

C  CALCULATE HTGR FOR REDWOOD
      ELSEIF(IMETH(ISP) .EQ. 3) THEN
        HTGR = EXP(-1.614631 - 0.000035*H**2 + 0.325206*LOG(H) 
     & - 0.001741*PBAL + 0.197669*LOG(CR*10) + 0.515935*LOG(SSITE)
     & - 0.001741*TEMSLP - 0.001886*(TEMSLP)*COS(TEMASP))

C  CONVERT 10-YEAR GROWTH TO 5-YEAR GROWTH
        HTGR = HTGR/2.0

C  CALCULATE HTGR FOR ALL OTHER SPECIES
      ELSE
        HTGR=HCON(ISP) + RELHT*HRELHT(ISP) + HCRSQ(ISP)*CR*CR
     &    + HHT(ISP) * H + HBA(ISP)*BAA + HSITE(ISP)*SSITE
      ENDIF
C----------
C  ROUTINE FOR HARDWOODS
C----------
      IF(HTGR .LE. 0.0)HTGR = 0.01
      YHTG=HTGR
      RETURN
      END
